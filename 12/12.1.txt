ORG	00H
JMP		INIT
ORG	0BH		;TIMER0
JMP		FANXIANG
ORG	13H		;INT1 外部中斷
JMP		COUNT
ORG	50H

INIT:
	MOV	IE,#10001110B		;EA - ET2 ES ET1 EX1 ET0 EX0
	MOV	IP,#00000100B		; -   - PT2 PS PT1 PX1 PT0 PX0
	MOV	TMOD,#11100010B		; Gate C/T M1 M0 Gate C/T M1 M0 	M1,M0決定MODE
													;timer counter 1    timer counter 0
	MOV	TL0,#227	;設定方波半週期
	MOV	TH0,#227
	MOV	TL1,#0		;計算距離
	MOV	TH1,#0
	
	CLR		P1.0			;方波一開始
	SETB		IT1			;FALLING EDGE TRIGGERED FOR INTERRUPT1
	SETB		TR0			;要讓ECHO來控制外部中斷
	SETB		TR1			;開始計數
	
	MOV	R0,#0		;個位數為0
	MOV	R1,#0		;十位數為0
	MOV	R2,#0		;百位數為0

;-----------顯示到七段顯示器-------------------
SHOW:
	MOV	A,R2			;百位數
	MOV	P2,A			;顯示在七段顯示器百位
	CALL	DELAY
	MOV	A,R1			;十位數
	MOV	P2,A			;顯示在七段顯示器十位
	CALL	DELAY
	MOV	A,R0			;個位數
	MOV	P2,A			;顯示在七段顯示器個位
	CALL	DELAY
	JMP		SHOW
;======================================
FANXIANG:
	CPL	P1.0		;反向以後COUNTER會加一
	CLR	TF0		;overflow flag of TIMER0
	RETI
COUNT:
	MOV	B,#100
	MOV	A,TL1
	DIV		AB		;A除以100
	ADD		A,#11010000B		;X N X X
	MOV	R2,A		;百位數
	MOV	A,B		;把十位數+個位數放到A
	MOV	B,#10
	DIV		AB
	ADD		A,#10110000B		;X X N X
	MOV	R1,A
	MOV	A,B
	ADD		A,#01110000B		;X X X N
	MOV	R0,A
	MOV	TH1,#0
	MOV	TL1,#0
	CLR		TF1
	RETI

DELAY:
	MOV	R6,#20
D:
	MOV	R7,#30
	DJNZ	R7,$
	DJNZ	R6,D
	RET
	
END	